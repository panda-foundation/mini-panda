namespace test;

public function test_statement()
{
    console.write_string("============ test   statement ============\n");
    // if
    var a u8 = 10;
    if (a >= 10)
    {
        assert(true, "a >= 10 should be true\n");
    }
    if (a < 10)
    {
        assert(false, "a < 10 shouldn't go here\n");
    }
    if (a > 100)
    {
        assert(false, "a > 100 shouldn't go here\n");
    }
    else
    {
        assert(true, "(a > 100) else should be true\n");
    }

    // for
    var count u8 = 0;
    for 
    {
        count++;
        if (count == 3)
        {
            break;
        }
    }
    assert(count == 3, "count should equal 3\n");
    for (count < 9)
    {
        count++;
    }
    assert(count == 9, "count should equal 9\n");
    for (var i u8 = 0; i < 10; i++)
    {
        if (i < 9)
        {
            continue;
        }
        count++;
    }
    assert(count == 10, "count should equal 10\n");

    // switch
    var timer Timer = Timer.timer1;
    timer = Timer.timer4;
    switch (timer)
    {
        case Timer.timer1, Timer.timer2:
            assert(false, "case timer1 or timer2, shouldn't go here");

        case Timer.timer3:
            assert(false, "case timer3, shouldn't go here");

        default:
            assert(true, "case timer4, should go here");
            count = 8;
    }
    assert(count == 8, "count should equal 8\n");
}
